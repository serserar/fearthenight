///////////////////////////////////////////////////////////////////////////////
//  Edit Map                                                                 //
///////////////////////////////////////////////////////////////////////////////
UI_mapscale = 10

UImenu "edit_map" [
   uivlist 0 [
      UIbutton "" [uitext "^f0Create New Map" 0.65] 0.4 0.05 [hideui "edit_map" ; showui "newmap"]
      uifill 0 0.03
      uihlist 0.01 [
         uivlist 0 [
            UItitle [uitext "Load Exisiting Map" 0.7] 0.3 0.03
            uihlist 0 [
               uiscroll 0.4 0.4 [
                  uifill 0.3 0.4
                  uivlist 0 [
                     uialign 0 -1
                     uiclamp 1 1
                     looplist m $mapfiles [
                        UIbutton (? (!=s $UI_editselmap $m) "hold2") [
                           uitext (getmapname $m) 0.6
                        ] 0 0.04 [
                           hideui "edit_map"
                           mode 0
                           map $m
                           edittoggle
                        ] -1 [uihover [UI_editselmap = $m]]
                     ]
                     uiclamp* 1 1
                  ]
               ]
               UIvscroll 0.02 0.4 1.6
            ]
         ]
         uivlist 0 [
            uialign 0 -1
            UItitle [uiclip 0.3 0 [uitext $UI_editselmap 0.7]] 0.3 0.03
            uimodcolor $c_boxmod 0.3 0.3 [
               uiclamp 1 1 1 1
               uiimage "media/interface/cube.png" 0.3 0.3 [uiimage (concatword "media/map/" $UI_editselmap ".png") 0.3 0.3 [uiclamp 1 1 1 1]]
            ]
            uifill 0.0 0.015
         ]
      ]
   ]
] [
   UI_editselmap = ""
] [] [] "Edit Map"

calcmapsize = [
   local side
   side = (* 25 (pow 2 (- $arg1 8)))
   concatword $side "x" $side
]

UImenu "newmap" [
   uispace 0.008 0.015 [
      uivlist 0 [
         uitext (concat "Size:" (calcmapsize $UI_mapscale) "meters") 0.6
         uifill 0 0.025
         uihlist 0.01 [
            UIhslider UI_mapscale 10 16 1 0.5 0.032 [uitext $UI_mapscale 0.6]
            uialign* -1 0
         ]
         uifill 0 0.025
         UIbutton "" [uitext (concatword (? (= $isconnected 1) (? (! $editing) "^f4")) "New") 0.6] 0.12 0.06 [
            hideui "newmap"
            newmap $UI_mapscale
            edittoggle true
         ]
      ]
   ]
] [] [] [] "New Map"
